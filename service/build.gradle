buildscript {
    ext {
        springBootVersion = '3.5.6'
        // Read version from library version.properties file
        def versionProps = new Properties()
        def versionFile = file('../library/src/main/resources/it/vige/cities/version.properties')
        if (!versionFile.exists()) {
            throw new GradleException("version.properties file not found at: ${versionFile.absolutePath}")
        }
        versionFile.withInputStream { versionProps.load(it) }
        citiesVersion = versionProps.getProperty('version')
        if (citiesVersion == null || citiesVersion.isEmpty()) {
            throw new GradleException("version property not found in version.properties file")
        }
        springdocVersion = '2.8.13'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java-library'
}

test {
    useJUnitPlatform()
    enabled = true
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'it.vige.cities'
version = "${citiesVersion}"

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    all {
        // Exclude Logback to avoid conflict with slf4j-simple in shaded JAR
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'ch.qos.logback', module: 'logback-core'
    }
}

dependencies {
    implementation("it.vige.cities:cities-generator:${citiesVersion}")
    // Use slf4j-simple (already included in shaded JAR) instead of Logback
    implementation "org.slf4j:slf4j-simple:2.0.17"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation("org.springframework.boot:spring-boot-starter-tomcat") {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'ch.qos.logback', module: 'logback-core'
    }
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.security:spring-security-oauth2-jose"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}