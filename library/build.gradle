buildscript {
    ext {
        citiesVersion = '1.2.4'
        slf4jVersion = '2.0.17'
        jsoupVersion = '1.21.2'
        commonsCliVersion = '1.10.0'
        commonsLangVersion = '3.19.0'
        resteasyVersion = '7.0.0.Final'
        junitVersion = '6.0.0'
        javaVersion = JavaVersion.VERSION_25
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

test {
    useJUnitPlatform()
}

apply plugin: 'java'
apply plugin: 'eclipse'

group = 'it.vige.cities'
version = "${citiesVersion}"

repositories {
    mavenCentral()
    maven {
        url = "https://www.aht-group.com/nexus/content/repositories/public"
    }
}

dependencies {
    api "org.slf4j:slf4j-simple:${slf4jVersion}"
    api "org.jsoup:jsoup:${jsoupVersion}"
    api "commons-cli:commons-cli:${commonsCliVersion}"
    api "org.apache.commons:commons-lang3:${commonsLangVersion}"
    api "org.jboss.resteasy:resteasy-jackson2-provider:${resteasyVersion}"
    api "org.jboss.resteasy:resteasy-client:${resteasyVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes("Main-Class": "it.vige.cities.Generator")
    }
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        sourceSets.main.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*'
    exclude 'META-INF/services/*'
    exclude 'module-info.class'
    exclude 'javax/activation/**.class'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Cities Generator'
                description = 'Generate a file with the cities of your country'
                url = 'http://cities-generator.vige.it'
                properties = [
                    'maven.compiler.source': "${javaVersion.majorVersion}",
                    'maven.compiler.target': "${javaVersion.majorVersion}"
                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'flashboss'
                        name = 'Luca Stancapiano'
                        email = 'luca.stancapiano@vige.it'
                        organization = 'vige'
                        organizationUrl = 'http://www.vige.it'
                        url = 'http://www.vige.it'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/flashboss/cities-generator.git'
                    developerConnection = 'scm:git:https://github.com/flashboss/cities-generator.git'
                    tag = '1.1.3'
                    url = 'https://github.com/flashboss/cities-generator'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

// Task to create ZIP bundle for manual publication on Central Publishing
task createPublishBundle(type: Zip) {
    dependsOn 'jar', 'javadocJar', 'sourcesJar', 'signMavenJavaPublication', 'generatePomFileForMavenJavaPublication'
    
    def artifactBaseName = 'cities-generator'
    def artifactVersion = version
    
    archiveBaseName = "${artifactBaseName}-bundle"
    archiveVersion = "${artifactVersion}"
    archiveExtension = 'zip'
    destinationDirectory = file("${buildDir}/distributions")
    
    // Include JAR and signatures from build/libs
    from("${buildDir}/libs") {
        include "${artifactBaseName}-${artifactVersion}.jar"
        include "${artifactBaseName}-${artifactVersion}.jar.asc"
        include "${artifactBaseName}-${artifactVersion}-sources.jar"
        include "${artifactBaseName}-${artifactVersion}-sources.jar.asc"
        include "${artifactBaseName}-${artifactVersion}-javadoc.jar"
        include "${artifactBaseName}-${artifactVersion}-javadoc.jar.asc"
    }
    
    // Include POM and signature from build/publications/mavenJava with rename
    from("${buildDir}/publications/mavenJava") {
        include "pom-default.xml"
        include "pom-default.xml.asc"
        rename "pom-default.xml", "${artifactBaseName}-${artifactVersion}.pom"
        rename "pom-default.xml.asc", "${artifactBaseName}-${artifactVersion}.pom.asc"
    }
    
    doLast {
        def bundleFile = archiveFile.get().asFile
        println "\nâœ… Bundle created: ${bundleFile}"
        println "ðŸ“¦ Size: ${bundleFile.length() / 1024} KB"
        println "ðŸ“¦ Files included:"
        println "   - cities-generator-${artifactVersion}.jar + .asc"
        println "   - cities-generator-${artifactVersion}-sources.jar + .asc"
        println "   - cities-generator-${artifactVersion}-javadoc.jar + .asc"
        println "   - cities-generator-${artifactVersion}.pom + .asc"
    }
}

// Execute createPublishBundle after publishToMavenLocal
tasks.named('publishToMavenLocal') {
    finalizedBy 'createPublishBundle'
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}