buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
	id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'java'
apply plugin: 'eclipse'

group = 'it.vige.cities'
version = '1.2.3'

sourceCompatibility = 20

repositories {
    mavenCentral()
    maven {
        url "https://www.aht-group.com/nexus/content/repositories/public"
    }
}

dependencies {
    api('org.slf4j:slf4j-simple:2.0.7')
    api('org.jsoup:jsoup:1.16.1')
    api('commons-cli:commons-cli:1.5.0')
    api('org.apache.commons:commons-lang3:3.12.0')
    api('org.jboss.resteasy:resteasy-jackson2-provider:6.2.3.Final')
    api('org.jboss.resteasy:resteasy-client:6.2.3.Final')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.9.3')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.9.3')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.9.3')
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes("Main-Class": "it.vige.cities.Generator")
    }
    from {
		duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        sourceSets.main.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*'
    exclude 'META-INF/services/*'
    exclude 'module-info.class'
    exclude 'javax/activation/**.class'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Cities Generator'
                description = 'Generate a file with the cities of your country'
                url = 'http://cities-generator.vige.it'
                properties = [
                    'maven.compiler.source': '20',
                    'maven.compiler.target': '20'
                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'flashboss'
                        name = 'Luca Stancapiano'
                        email = 'luca.stancapiano@vige.it'
                        organization = 'vige'
                        organizationUrl = 'http://www.vige.it'
                        url = 'http://www.vige.it'
                    }
                }
                scm {
                    connection = 'svn:git:https://github.com/flashboss/cities-generator.git'
                    developerConnection = 'svn:git:https://github.com/flashboss/cities-generator.git'
                    tag = '1.1.3'
                    url = 'svn:git:https://github.com/flashboss/cities-generator.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username 'luca.stancapiano'
                password System.properties['mavenPassword']
            }
        }
    }
}

signing {
	useGpgCmd()
	sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
