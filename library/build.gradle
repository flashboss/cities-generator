buildscript {
    ext {
        // Read version from version.properties file
        def versionProps = new Properties()
        def versionFile = file('src/main/resources/it/vige/cities/version.properties')
        if (!versionFile.exists()) {
            throw new GradleException("version.properties file not found at: ${versionFile.absolutePath}")
        }
        versionFile.withInputStream { versionProps.load(it) }
        citiesVersion = versionProps.getProperty('version')
        if (citiesVersion == null || citiesVersion.isEmpty()) {
            throw new GradleException("version property not found in version.properties file")
        }
        slf4jVersion = '2.0.17'
        jsoupVersion = '1.21.2'
        commonsCliVersion = '1.10.0'
        commonsLangVersion = '3.19.0'
        resteasyVersion = '7.0.0.Final'
        junitVersion = '6.0.0'
        javaVersion = JavaVersion.VERSION_25
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

test {
    useJUnitPlatform()
}

apply plugin: 'java'
apply plugin: 'eclipse'

group = 'it.vige.cities'
version = "${citiesVersion}"

repositories {
    mavenCentral()
    maven {
        url = "https://www.aht-group.com/nexus/content/repositories/public"
    }
}

dependencies {
    api "org.slf4j:slf4j-simple:${slf4jVersion}"
    api "org.jsoup:jsoup:${jsoupVersion}"
    api "commons-cli:commons-cli:${commonsCliVersion}"
    api "org.apache.commons:commons-lang3:${commonsLangVersion}"
    api "org.jboss.resteasy:resteasy-jackson2-provider:${resteasyVersion}"
    api "org.jboss.resteasy:resteasy-client:${resteasyVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    withJavadocJar()
    withSourcesJar()
}


// JAR configuration - includes all dependencies (shaded)
jar {
    manifest {
        attributes("Main-Class": "it.vige.cities.Generator")
        attributes("Implementation-Version": "${citiesVersion}")
    }
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        sourceSets.main.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/versions/**'
    exclude 'module-info.class'
    exclude 'javax/activation/**.class'
}

// Apply publishing configuration
apply from: 'publish.gradle'

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}